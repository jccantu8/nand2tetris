// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Math.jack

/**
 * A library of commonly used mathematical functions.
 * Note: Jack compilers implement multiplication and division using OS method calls.
 */
class Math {
    static Array twoToThe;

    /** Initializes the library. */
    function void init() {
        //Initialize auxiliary array for multiply()
        let twoToThe = Array.new(16);
        let twoToThe[0] = 1;
        let twoToThe[1] = 2;
        let twoToThe[2] = 4;
        let twoToThe[3] = 8;
        let twoToThe[4] = 16;
        let twoToThe[5] = 32;
        let twoToThe[6] = 64;
        let twoToThe[7] = 128;
        let twoToThe[8] = 256;
        let twoToThe[9] = 512;
        let twoToThe[10] = 1024;
        let twoToThe[11] = 2048;
        let twoToThe[12] = 4096;
        let twoToThe[13] = 8192;
        let twoToThe[14] = 16384;
        let twoToThe[15] = 32767 + 1;

        return;
    }

    /** Returns the absolute value of x. */
    function int abs(int x) {
        if (x > -1) {
            return x;
        } else {
            return -x;
        }
    }

    /** Returns the product of x and y. 
     *  When a Jack compiler detects the multiplication operator '*' in the 
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x*y and multiply(x,y) return the same value.
     */
    function int multiply(int x, int y) {
        var int sum, i, shiftedX;

        let i = 0;
        let sum = 0;
        let shiftedX = x;

        while (i < 16) {
            if (Math.bit(y, i)) {
                let sum = sum + shiftedX;
            }

            let shiftedX = shiftedX + shiftedX;
            let i = i + 1;
        }
        
        return sum;
    }

    /** Returns the integer part of x/y.
     *  When a Jack compiler detects the multiplication operator '/' in the 
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x/y and divide(x,y) return the same value.
     */
    function int divide(int x, int y) {
        var int q, k;
        var boolean nonNegative;

        if (x = 0) {
            return 0;
        }

        if ((x < 0) & (y < 0)) {
            let nonNegative = true;
        }
        if ((x < 0) & (y > 0)) {
            let nonNegative = false;
        }
        if ((x > 0) & (y < 0)) {
            let nonNegative = false;
        }
        if ((x > 0) & (y > 0)) {
            let nonNegative = true;
        }

        let x = Math.abs(x);
        let y = Math.abs(y);

        if ((y > x) | (y < 0)) {
            return 0;
        } else {
            let q = Math.divide(x, 2 * y);

            if (x - (2 * q * y) < y) {
                let k = 2 * q;
            } else {
                let k = (2 * q) + 1;
            }

            if (nonNegative) {
                return k;
            } else {
                return k * -1;
            }
        }
    }

    /** Returns the integer part of the square root of x. */
    function int sqrt(int x) {
        var int y, i, m, n;

        let y = 0;
        let i = (16 / 2) - 1;

        while (i > -1) {
            let n = y + Math.pow(2, i);
            let m = Math.pow(n, 2);

            if (((m < x) | (m = x)) & (m > -1)) {
                let y = n;
            }

            let i = i - 1;
        }

        return y;
    }

    /** Returns x to the power of y */
    function int pow(int x, int y) {
        var int sum;

        if (y = 0) {
            return 1;
        }

        let sum = x;

        while (y > 1) {
            let sum = sum * x;

            let y = y - 1;
        }

        return sum;
    }

    /** Returns the greater number. */
    function int max(int a, int b) {
        if (a > b) {
            return a;
        } else {
            return b;
        }
    }

    /** Returns the smaller number. */
    function int min(int a, int b) {
        if (a < b) {
            return a;
        } else {
            return b;
        }
    }

    // Private

    function boolean bit(int x, int i) {
        if (twoToThe[i] & x = 0) {
            return false;
        } else {
            return true;
        }
    }
}
